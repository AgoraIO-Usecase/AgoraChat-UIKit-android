group = "io.hyphenate"
archivesBaseName = "ease-chat-kit"
version = "4.5.0"//发布aar的库版本

apply {
    plugin 'maven-publish'
    plugin 'signing'
}


def localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

Properties properties = new Properties()
properties.load(file(localProperties.getProperty('maven.dir')+"./project.properties").newDataInputStream())

def ossrhUsername = properties.getProperty('ossrhUsername')
def ossrhPassword = properties.getProperty('ossrhPassword')

//指定singing的相关信息
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        def id = properties.getProperty("signing.keyId")
        def file = localProperties.getProperty('maven.dir')+properties.getProperty("signing.secretKeyRingFile")
        def password = properties.getProperty("signing.password")

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }
    }
}

configurations {
    deployerJars
}

task javadoc(type: Javadoc) {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
    }
    source = android.sourceSets.main.kotlin.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.source = variant.javaCompiler.source
            owner.classpath = files(android.bootClasspath.join(File.pathSeparator))
            owner.classpath += variant.javaCompiler.classpath
            owner.include "**/*.kt"
        }
    }
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    // classifier = 'javadoc'
    // 8.0 使用 archiveClassifier 代替 classifier
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    //from android.sourceSets.main.kotlin.srcDirs
    //def dirs = android.sourceSets.getByName("main").java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
    //classifier = 'sources'
    // 8.0 使用 archiveClassifier 代替 classifier
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = group
                artifactId = archivesBaseName
                version = version

                afterEvaluate {
                    from components.release
                }
                // 设置要发布的文件
                artifact("$buildDir/libs/$archivesBaseName-$version-javadoc.jar"){
                    classifier 'javadoc'
                }
                artifact("$buildDir/libs/$archivesBaseName-$version-sources.jar"){
                    classifier 'sources'
                }
                signing.sign publishing.publications.release

                pom {
                    name = archivesBaseName
                    description = "Easy UI for Easemob IM SDK"
                    url = "https://github.com/easemob/chatuikit-android"

                    scm {
                        connection = "scm:git@github.com:easemob/chatuikit-android.git"
                        developerConnection = "scm:git@github.com:easemob/chatuikit-android.git"
                        url = "https://github.com/easemob/chatuikit-android.git"
                    }

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "Easemob"
                            name = "Easemob"
                            email = "sdk-tools@easemob.com"
                        }
                    }
                }

            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
//        maven {
//            name = "ossrh-snapshot"
//            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
//            credentials {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
//        }
    }
}